apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

def siteUrl = 'https://github.com/tomvlk/MPFormatter_android'
def gitUrl = 'https://github.com/tomvlk/MPFormatter_android.git'

def libGroup = 'net.tvalk.mpformatter'
def libVersion = '1.3.6-SNAPSHOT'

group = libGroup
version = libVersion

install {
    repositories.mavenInstaller {
        pom.project {
            packaging = 'aar'

            name = 'Maniaplanet Style Parser'
            description = 'Maniaplanet dollar style android parser can be used to convert Maniaplanet' +
                    'styles to spannablestrings that you can use in styled spannable textviews.'

            url = siteUrl

            version = libVersion
            groupId = libGroup

            licenses {
                license {
                    name = 'MIT'
                    url = 'http://opensource.org/licenses/MIT'
                }
            }

            developers {
                developer {
                    id = 'tomvlk'
                    name = 'Tom Valk'
                    email = 'info@lt-box.info'
                }
            }

            scm {
                connection = gitUrl
                developerConnection = gitUrl
                url = siteUrl
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

if (project.rootProject.file("local.properties").exists()) {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())

    bintray {
        user = properties.getProperty("bintray.user")
        key = properties.getProperty("bintray.key")

        configurations = ['archives']
        dryRun = false
        publish = true

        pkg {
            repo = "maven"
            name = "mpformatter"
            websiteUrl = siteUrl
            vcsUrl = gitUrl
            licenses = ["MIT"]

            dryRun = false
            publish = true
        }
    }
}
